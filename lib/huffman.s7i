
(********************************************************************)
(*                                                                  *)
(*  huffman.s7i   Support for Huffman coding.                       *)
(*  Copyright (C) 2021, 2022  Thomas Mertes                         *)
(*                                                                  *)
(*  This file is part of the Seed7 Runtime Library.                 *)
(*                                                                  *)
(*  The Seed7 Runtime Library is free software; you can             *)
(*  redistribute it and/or modify it under the terms of the GNU     *)
(*  Lesser General Public License as published by the Free Software *)
(*  Foundation; either version 2.1 of the License, or (at your      *)
(*  option) any later version.                                      *)
(*                                                                  *)
(*  The Seed7 Runtime Library is distributed in the hope that it    *)
(*  will be useful, but WITHOUT ANY WARRANTY; without even the      *)
(*  implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR *)
(*  PURPOSE.  See the GNU Lesser General Public License for more    *)
(*  details.                                                        *)
(*                                                                  *)
(*  You should have received a copy of the GNU Lesser General       *)
(*  Public License along with this program; if not, write to the    *)
(*  Free Software Foundation, Inc., 51 Franklin Street,             *)
(*  Fifth Floor, Boston, MA  02110-1301, USA.                       *)
(*                                                                  *)
(********************************************************************)


include "bitdata.s7i";


const type: huffmanTable is sub object interface;


const func integer: getHuffmanSymbol (inout bitStream: inBitStream,
                                      in huffmanTable: table) is DYNAMIC;


const type: msbHuffmanTable is new struct
    var integer: maxBitWidth is 0;
    var array integer: symbols is 0 times 0;
    var array integer: codeLengths is 0 times 0;
  end struct;

type_implements_interface(msbHuffmanTable, huffmanTable);

const type: valuesOfCodeWithLengthType is array array integer;


(**
 *  Create a Huffman table for reading in MSB-First order.
 *  This Huffman encoding is used by JPEG files. It can happen that
 *  ''huffmanValues'' contains the same value twice. In that case
 *  the same symbol is encoded in two ways. This makes absolutely no
 *  sense but it can happen. For that reason it is necessary to use
 *  table.codeLengths with the same index as table.symbols.
 *  @param numberOfCodesWithLength Array to map bit width to number
 *                                 of symbols that use this bit width.
 *  @param huffmanValues String with symbols ordered by the bit width.
 *)
const func msbHuffmanTable: createHuffmanTableMsb (in integer: maximumCodeLength,
    in array integer: numberOfCodesWithLength, in string: huffmanValues) is func
  result
    var msbHuffmanTable: table is msbHuffmanTable.value;
  local
    var integer: currentCode is 0;
    var integer: codeLength is 0;
    var integer: valueIndexStart is 0;
    var integer: valueIndexEnd is 0;
    var integer: valueIndex is 0;
    var integer: huffmanValue is 0;
    var integer: tableIndex is 0;
  begin
    table.maxBitWidth := maximumCodeLength;
    table.symbols := [0 .. pred(1 << maximumCodeLength)] times 0;
    table.codeLengths := [0 .. pred(1 << maximumCodeLength)] times 0;
    for codeLength range 1 to maximumCodeLength do
      valueIndexStart := succ(valueIndexEnd);
      valueIndexEnd +:= numberOfCodesWithLength[codeLength];
      for valueIndex range valueIndexStart to valueIndexEnd do
        huffmanValue := ord(huffmanValues[valueIndex]);
        while currentCode = tableIndex >> (maximumCodeLength - codeLength) do
          table.symbols[tableIndex] := huffmanValue;
          table.codeLengths[tableIndex] := codeLength;
          incr(tableIndex);
        end while;
        incr(currentCode);
      end for;
      currentCode <<:= 1;
    end for;
    huffmanValue := ord(huffmanValues[length(huffmanValues)]);
    while tableIndex <= pred(1 << maximumCodeLength) do
      table.symbols[tableIndex] := ord(huffmanValues[length(huffmanValues)]);
      table.codeLengths[tableIndex] := maximumCodeLength;
      incr(tableIndex);
    end while;
  end func;


(**
 *  Get a huffman symbol from ''inBitStream'' in MSB-First order using the huffman ''table''.
 *  The read direction is from MSB (most significant bit) to LSB (least significant bit).
 *  The function peeks bits from ''inBitStream''. By default ''inBitStream'' appends some
 *  '\16#ff;' bytes to allow that bits can be peeked always.
 *  @param inBitStream MSB orderd bit stream from which the bits are skipped.
 *  @param table Huffman table defining the bit sequences that encode the symbols.
 *)
const func integer: getHuffmanSymbol (inout msbBitStream: inBitStream,
    in msbHuffmanTable: table) is func
  result
    var integer: symbol is 0;
  local
    var integer: index is 0;
  begin
    index := peekBits(inBitStream, table.maxBitWidth);
    symbol := table.symbols[index];
    skipBits(inBitStream, table.codeLengths[index]);
  end func;


# The function getHuffmanSymbolMsb(string) is deprecated. Use getHuffmanSymbol(msbBitStream) instead.
const func integer: getHuffmanSymbolMsb (in string: stri, inout integer: bytePos,
    inout integer: bitPos, in msbHuffmanTable: table) is func
  result
    var integer: symbol is 0;
  local
    var integer: index is 0;
  begin
    index := peekBitsMsb(stri, bytePos, bitPos, table.maxBitWidth);
    symbol := table.symbols[index];
    skipBitsMsb(bytePos, bitPos, table.codeLengths[index]);
  end func;


const type: lsbHuffmanTable is new struct
    var integer: maxBitWidth is 0;
    var array integer: symbols is 0 times 0;
    var array integer: codeLengths is 0 times 0;
  end struct;

type_implements_interface(lsbHuffmanTable, huffmanTable);


const func lsbHuffmanTable: createHuffmanTableLsb (in array integer: codeLengths,
    in integer: maximumCodeLength,
    in valuesOfCodeWithLengthType: valuesOfCodeWithLength) is func
  result
    var lsbHuffmanTable: table is lsbHuffmanTable.value;
  local
    var integer: codeLength is 0;
    var integer: huffmanValue is 0;
    var integer: currentCode is 0;
    var integer: reversedCode is 0;
    var integer: highBits is 0;
  begin
    table.maxBitWidth := maximumCodeLength;
    table.symbols := [0 .. pred(1 << maximumCodeLength)] times 0;
    table.codeLengths := codeLengths;
    for codeLength range 1 to maximumCodeLength do
      for huffmanValue range valuesOfCodeWithLength[codeLength] do
        reversedCode := reverseBits(codeLength, currentCode);
        for highBits range 0 to pred(1 << maximumCodeLength) step 1 << codeLength do
          table.symbols[highBits + reversedCode] := huffmanValue;
        end for;
        incr(currentCode);
      end for;
      currentCode <<:= 1;
    end for;
  end func;


(**
 *  Create a Huffman table for reading in LSB-First order.
 *  This Huffman encoding is used by the deflate algorithm.
 *  E.g.: The code lengths (in bits) of
 *   4 0 0 6 5 3 3 3 3 3 4 3 0 0 0 0 5 5 6
 *  describe that 0 is encoded with 4 bits, 3 with 6 bits, etc.
 *  This leads to the following encoding lengths:
 *   length 3: (5, 6, 7, 8, 9, 11)
 *   length 4: (0, 10)
 *   length 5: (4, 16, 17)
 *   length 6: (3, 18)
 *  Beginning with the lowest length the following encodings are generated:
 *   000: 5
 *   001: 6
 *   ...
 *   101: 11
 *  For the next length (4 instead of 3) the value is incremented and shifted:
 *   1100: 0
 *  The encoder should be able fetch the maximum length of bits and to
 *  use it as index. In order to allow that the data must be transformed.
 *  The bits must be flipped and all variants of higher bits must be added:
 *   000000 encodes 5
 *   000001 encodes 9
 *   000010 encodes 7
 *   000011 encodes 0
 *   000100 encodes 6
 *   ...
 *   001000 encodes 5
 *   001001 encodes 9
 *   001010 encodes 7
 *   ...
 *  @param codeLengths Array to map the codes to the number of bits used
 *                     to encode this code. Zero means: This code is not used.
 *)
const func lsbHuffmanTable: createHuffmanTableLsb (in array integer: codeLengths) is func
  result
    var lsbHuffmanTable: table is lsbHuffmanTable.value;
  local
    var integer: codeLength is 0;
    var integer: maximumCodeLength is 0;
    var integer: numberOfCodes is 0;
    var array integer: numberOfCodesWithLength is 0 times 0;
    var valuesOfCodeWithLengthType: valuesOfCodeWithLength is 0 times 0 times 0;
    var array integer: valueIndex is 0 times 0;
    var integer: number is 0;
    var integer: code is 0;
  begin
    for codeLength range codeLengths do
      if codeLength > maximumCodeLength then
        maximumCodeLength := codeLength;
      end if;
    end for;
    numberOfCodesWithLength := [1 .. maximumCodeLength] times 0;
    for codeLength range codeLengths do
      if codeLength <> 0 then
        incr(numberOfCodesWithLength[codeLength]);
      end if;
    end for;
    valuesOfCodeWithLength := [1 .. maximumCodeLength] times 0 times 0;
    for numberOfCodes key codeLength range numberOfCodesWithLength do
      if numberOfCodes <> 0 then
        valuesOfCodeWithLength[codeLength] := numberOfCodes times 0;
      end if;
    end for;
    valueIndex := [1 .. maximumCodeLength] times 1;
    for codeLength key code range codeLengths do
      if codeLength <> 0 then
        valuesOfCodeWithLength[codeLength][valueIndex[codeLength]] := code;
        incr(valueIndex[codeLength]);
      end if;
    end for;
    table := createHuffmanTableLsb(codeLengths, maximumCodeLength,
                                   valuesOfCodeWithLength);
  end func;


(**
 *  Get a huffman symbol from ''inBitStream'' in LSB-First order using the huffman ''table''.
 *  The read direction is from LSB (least significant bit) to MSB (most significant bit).
 *  The function peeks bits from ''inBitStream''. By default ''inBitStream'' appends some
 *  '\16#ff;' bytes to allow that bits can be peeked always.
 *  @param inBitStream LSB orderd bit stream from which the bits are skipped.
 *  @param table Huffman table defining the bit sequences that encode the symbols.
 *)
const func integer: getHuffmanSymbol (inout lsbBitStream: inBitStream,
    in lsbHuffmanTable: table) is func
  result
    var integer: symbol is 0;
  local
    var integer: index is 0;
  begin
    index := peekBits(inBitStream, table.maxBitWidth);
    symbol := table.symbols[index];
    skipBits(inBitStream, table.codeLengths[symbol]);
  end func;


# The function getHuffmanSymbolLsb(string) is deprecated. Use getHuffmanSymbol(lsbBitStream) instead.
const func integer: getHuffmanSymbolLsb (in string: stri, inout integer: bytePos,
    inout integer: bitPos, in lsbHuffmanTable: table) is func
  result
    var integer: symbol is 0;
  local
    var integer: index is 0;
  begin
    index := peekBitsLsb(stri, bytePos, bitPos, table.maxBitWidth);
    symbol := table.symbols[index];
    skipBitsLsb(bytePos, bitPos, table.codeLengths[symbol]);
  end func;
