
(********************************************************************)
(*                                                                  *)
(*  deflate.s7i   Deflate compression algorithm                     *)
(*  Copyright (C) 2013  Thomas Mertes                               *)
(*                                                                  *)
(*  This file is part of the Seed7 Runtime Library.                 *)
(*                                                                  *)
(*  The Seed7 Runtime Library is free software; you can             *)
(*  redistribute it and/or modify it under the terms of the GNU     *)
(*  Lesser General Public License as published by the Free Software *)
(*  Foundation; either version 2.1 of the License, or (at your      *)
(*  option) any later version.                                      *)
(*                                                                  *)
(*  The Seed7 Runtime Library is distributed in the hope that it    *)
(*  will be useful, but WITHOUT ANY WARRANTY; without even the      *)
(*  implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR *)
(*  PURPOSE.  See the GNU Lesser General Public License for more    *)
(*  details.                                                        *)
(*                                                                  *)
(*  You should have received a copy of the GNU Lesser General       *)
(*  Public License along with this program; if not, write to the    *)
(*  Free Software Foundation, Inc., 51 Franklin Street,             *)
(*  Fifth Floor, Boston, MA  02110-1301, USA.                       *)
(*                                                                  *)
(********************************************************************)


const proc: putBitLe (inout string: stri, inout integer: bitPos, in integer: bit) is func
  begin
    if bitPos = 8 then
      stri &:= chr(bit);
      bitPos := 1;
    else
      stri @:= [length(stri)] chr(ord(stri[length(stri)]) + (bit << bitPos));
      incr(bitPos);
    end if;
  end func;


const proc: putBitsLe (inout string: stri, inout integer: bitPos, in var integer: bits,
    in var integer: bitsToWrite) is func
  local
    var integer: bitsFree is 0;
  begin
    bitsFree := 8 - bitPos;
    if bitsFree > bitsToWrite then
      stri @:= [length(stri)] chr(ord(stri[length(stri)]) + (bits << bitPos));
      bitPos +:= bitsToWrite;
    else
      if bitsFree >= 1 then
        stri @:= [length(stri)] chr(ord(stri[length(stri)]) + ((bits rem (1 << bitsFree)) << bitPos));
        bits >>:= bitsFree;
        bitsToWrite -:= bitsFree;
      end if;
      while bitsToWrite >= 8 do
        stri &:= chr(bits rem 256);
        bits >>:= 8;
        bitsToWrite -:= 8;
      end while;
      if bitsToWrite >= 1 then
        stri &:= chr(bits);
        bitPos := bitsToWrite;
      else
        bitPos := 8;
      end if;
    end if;
  end func;


const func integer: reverseBits (in var integer: bits, in integer: size) is func
  result
    var integer: reversed is 0;
  local
    var integer: bitPos is 0;
  begin
    for bitPos range 1 to size do
      reversed <<:= 1;
      reversed +:= bits mod 2;
      bits >>:= 1;
    end for;
  end func;


const proc: putLiteralOrLength (inout string: stri, inout integer: bitPos,
    in integer: literalOrLength) is func
  begin
    if literalOrLength <= 143 then
      putBitsLe(stri, bitPos, reverseBits(literalOrLength + 2#00110000, 8), 8);
    elsif literalOrLength <= 255 then
      putBitsLe(stri, bitPos, reverseBits(literalOrLength + 2#110010000 - 144, 9), 9);
    elsif literalOrLength <= 279 then
      putBitsLe(stri, bitPos, reverseBits(literalOrLength - 256, 7), 7);
    else
      putBitsLe(stri, bitPos, reverseBits(literalOrLength + 2#11000000 - 280, 8), 8);
    end if;
  end func;


const proc: putLength (inout string: stri, inout integer: bitPos, in integer: length) is func
  begin
    if length <= 10 then
      putLiteralOrLength(stri, bitPos, 254 + length);
    elsif length <= 18 then
      putLiteralOrLength(stri, bitPos, 265 + ((length - 11) >> 1));
      putBitLe(stri, bitPos, (length - 11) rem 2);
    elsif length <= 34 then
      putLiteralOrLength(stri, bitPos, 269 + ((length - 19) >> 2));
      putBitsLe(stri, bitPos, (length - 19) rem 4, 2);
    elsif length <= 66 then
      putLiteralOrLength(stri, bitPos, 273 + ((length - 35) >> 3));
      putBitsLe(stri, bitPos, (length - 35) rem 8, 3);
    elsif length <= 130 then
      putLiteralOrLength(stri, bitPos, 277 + ((length - 67) >> 4));
      putBitsLe(stri, bitPos, (length - 67) rem 16, 4);
    elsif length <= 257 then
      putLiteralOrLength(stri, bitPos, 281 + ((length - 131) >> 5));
      putBitsLe(stri, bitPos, (length - 131) rem 32, 5);
    elsif length = 258 then
      putLiteralOrLength(stri, bitPos, 285);
    else
      raise RANGE_ERROR;
    end if;
  end func;


const proc: putDistance (inout string: stri, inout integer: bitPos, in integer: distance) is func
  begin
    if distance <= 4 then
      putBitsLe(stri, bitPos, reverseBits(pred(distance), 5), 5);
    elsif distance <= 8 then
      putBitsLe(stri, bitPos, reverseBits(4 + ((distance - 5) >> 1), 5), 5);
      putBitLe(stri, bitPos, (distance - 5) rem 2);
    elsif distance <= 16 then
      putBitsLe(stri, bitPos, reverseBits(6 + ((distance - 9) >> 2), 5), 5);
      putBitsLe(stri, bitPos, (distance - 9) rem 4, 2);
    elsif distance <= 32 then
      putBitsLe(stri, bitPos, reverseBits(8 + ((distance - 17) >> 3), 5), 5);
      putBitsLe(stri, bitPos, (distance - 17) rem 8, 3);
    elsif distance <= 64 then
      putBitsLe(stri, bitPos, reverseBits(10 + ((distance - 33) >> 4), 5), 5);
      putBitsLe(stri, bitPos, (distance - 33) rem 16, 4);
    elsif distance <= 128 then
      putBitsLe(stri, bitPos, reverseBits(12 + ((distance - 65) >> 5), 5), 5);
      putBitsLe(stri, bitPos, (distance - 65) rem 32, 5);
    elsif distance <= 256 then
      putBitsLe(stri, bitPos, reverseBits(14 + ((distance - 129) >> 6), 5), 5);
      putBitsLe(stri, bitPos, (distance - 129) rem 64, 6);
    elsif distance <= 512 then
      putBitsLe(stri, bitPos, reverseBits(16 + ((distance - 257) >> 7), 5), 5);
      putBitsLe(stri, bitPos, (distance - 257) rem 128, 7);
    elsif distance <= 1024 then
      putBitsLe(stri, bitPos, reverseBits(18 + ((distance - 513) >> 8), 5), 5);
      putBitsLe(stri, bitPos, (distance - 513) rem 256, 8);
    elsif distance <= 2048 then
      putBitsLe(stri, bitPos, reverseBits(20 + ((distance - 1025) >> 9), 5), 5);
      putBitsLe(stri, bitPos, (distance - 1025) rem 512, 9);
    elsif distance <= 4096 then
      putBitsLe(stri, bitPos, reverseBits(22 + ((distance - 2049) >> 10), 5), 5);
      putBitsLe(stri, bitPos, (distance - 2049) rem 1024, 10);
    elsif distance <= 8192 then
      putBitsLe(stri, bitPos, reverseBits(24 + ((distance - 4097) >> 11), 5), 5);
      putBitsLe(stri, bitPos, (distance - 4097) rem 2048, 11);
    elsif distance <= 16384 then
      putBitsLe(stri, bitPos, reverseBits(26 + ((distance - 8193) >> 12), 5), 5);
      putBitsLe(stri, bitPos, (distance - 8193) rem 4096, 12);
    elsif distance <= 32768 then
      putBitsLe(stri, bitPos, reverseBits(28 + ((distance - 16385) >> 13), 5), 5);
      putBitsLe(stri, bitPos, (distance - 16385) rem 8192, 13);
    else
      raise RANGE_ERROR;
    end if;
  end func;


const proc: deflateOne (in string: uncompressed, inout integer: uPos,
    inout string: compressed, inout integer: bitPos) is func
  local
    var integer: limit is 1;
    var integer: maxLength is 258;
    var string: searched is "";
    var integer: posFound is 0;
    var integer: length is 0;
    var integer: lengthFound is 0;
    var integer: distanceFound is 0;
  begin
    if uPos > 32768 then
      limit := uPos - 32768;
    end if;
    if length(uncompressed) - uPos < maxLength then
      maxLength := length(uncompressed) - uPos;
    end if;
    searched := uncompressed[uPos len 3];
    lengthFound := length(searched);
    if lengthFound >= 3 then
      posFound := pos(uncompressed, searched, limit);
      if posFound < uPos then
        repeat
          length := lengthFound;
          while length < maxLength and
                uncompressed[uPos + length] = uncompressed[posFound + length] do
            incr(length);
          end while;
          lengthFound := succ(length);
          searched := uncompressed[uPos len lengthFound];
          if lengthFound <= maxLength then
            posFound := pos(uncompressed, searched, succ(posFound));
          end if;
        until posFound >= uPos or lengthFound > maxLength;
        decr(lengthFound);
        searched := uncompressed[uPos len lengthFound];
        distanceFound := uPos - rpos(uncompressed, searched, pred(uPos));
      end if;
    end if;
    if distanceFound <> 0 then
      # writeln("length: " <& lengthFound);
      putLength(compressed, bitPos, lengthFound);
      putDistance(compressed, bitPos, distanceFound);
      uPos +:= lengthFound;
    else
      putLiteralOrLength(compressed, bitPos, ord(uncompressed[uPos]));
      incr(uPos);
    end if;
  end func;


const proc: beginDeflateBlock (inout string: compressed, inout integer: bitPos,
    in boolean: bfinal) is func
  begin
    putBitLe(compressed, bitPos, ord(bfinal));
    putBitsLe(compressed, bitPos, 1, 2);       # btype:  Fixed Huffman codes
  end func;


const proc: endDeflateBlock (inout string: compressed, inout integer: bitPos) is func
  begin
    putLiteralOrLength(compressed, bitPos, 256);
  end func;


const proc: closeDeflateBlock (in string: uncompressed, inout integer: uPos,
    inout string: compressed, inout integer: bitPos) is func
  begin
    while uPos <= length(uncompressed) do
      deflateOne(uncompressed, uPos, compressed, bitPos);
    end while;
    endDeflateBlock(compressed, bitPos);
  end func;


const proc: addDeflateData (in string: uncompressed, inout integer: uPos,
    inout string: compressed, inout integer: bitPos) is func
  begin
    while uPos <= length(uncompressed) - 258 do
      deflateOne(uncompressed, uPos, compressed, bitPos);
    end while;
  end func;


(**
 *  Compress a string with the DEFLATE algorithm.
 *  DEFLATE is a compression algorithm that uses a combination of
 *  the LZ77 algorithm and Huffman coding.
 *)
const proc: deflateBlock (in string: uncompressed, inout string: compressed,
    inout integer: bitPos, in boolean: bfinal) is func
  local
    var integer: uPos is 1;
  begin
    beginDeflateBlock(compressed, bitPos, bfinal);
    while uPos <= length(uncompressed) do
      deflateOne(uncompressed, uPos, compressed, bitPos);
    end while;
    endDeflateBlock(compressed, bitPos);
  end func;


const proc: deflateBlock (inout file: inFile, inout string: compressed,
    inout integer: bitPos, in boolean: bfinal) is func
  local
    var string: uncompressed is "";
    var integer: uPos is 1;
  begin
    putBitLe(compressed, bitPos, ord(bfinal));
    putBitsLe(compressed, bitPos, 1, 2);       # btype:  Fixed Huffman codes
    uncompressed := gets(inFile, 258);
    while uPos <= length(uncompressed) do
      deflateOne(uncompressed, uPos, compressed, bitPos);
      uncompressed &:= gets(inFile, 257 - length(uncompressed) + uPos);
    end while;
    putLiteralOrLength(compressed, bitPos, 256);
  end func;
